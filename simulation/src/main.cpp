#include <Adafruit_MPU6050.h>
#include <Wire.h>
#include <math.h>

Adafruit_MPU6050 mpu1; // capteur haut du dos
Adafruit_MPU6050 mpu2; // capteur bas du dos

void setup(void) {
  Serial.begin(115200);

  if (!mpu1.begin(0x68)) { while (1) yield(); }
  if (!mpu2.begin(0x69)) { while (1) yield(); }
  
  mpu1.setAccelerometerRange(MPU6050_RANGE_2_G);
  mpu2.setAccelerometerRange(MPU6050_RANGE_2_G);
}

void printData(sensors_event_t &a, sensors_event_t &g) {
  Serial.print("\"accX\":"); Serial.print(a.acceleration.x, 2);
  Serial.print(",\"accY\":"); Serial.print(a.acceleration.y, 2);
  Serial.print(",\"accZ\":"); Serial.print(a.acceleration.z, 2);
  Serial.print(",\"gyrX\":"); Serial.print(g.gyro.x, 2);
  Serial.print(",\"gyrY\":"); Serial.print(g.gyro.y, 2);
  Serial.print(",\"gyrZ\":"); Serial.print(g.gyro.z, 2);
}

void loop() {
  sensors_event_t a1, g1, t1;
  sensors_event_t a2, g2, t2;

  unsigned long timestamp = millis();
  mpu1.getEvent(&a1, &g1, &t1);
  mpu2.getEvent(&a2, &g2, &t2);

  float pitchHigh = atan2(-a1.acceleration.x, sqrt(pow(a1.acceleration.y, 2) + pow(a1.acceleration.z, 2))) * 180.0 / M_PI;
  // float rollHigh  = atan2(a1.acceleration.y, a1.acceleration.z) * 180.0 / M_PI;
  
  float pitchLow  = atan2(-a2.acceleration.x, sqrt(pow(a2.acceleration.y, 2) + pow(a2.acceleration.z, 2))) * 180.0 / M_PI;
  float rollLow   = atan2(a2.acceleration.y, a2.acceleration.z) * 180.0 / M_PI;

  String activity = "UNKNOWN";
  float accZ_g = a2.acceleration.z / 9.81;

  if (abs(pitchLow) < 15 && abs(rollLow) < 15) {
    activity = "STAND_UP";
  } else if ((pitchLow > 50 && pitchLow < 130) || (rollLow > 50 && rollLow < 130)) {
    activity = "SIT_DOWN";
  } else if (abs(pitchLow) > 60 || abs(accZ_g) < 0.3) {
    activity = "LAY_DOWN";
  }

  float deltaAngle = abs(pitchHigh - pitchLow);
  String posture = "GOOD_POSTURE";
  float badPostureFloor = 25.0; // Seuil de 25 degrés de différence

  if (activity == "STAND_UP" || activity == "SIT_DOWN") {
    if (deltaAngle > badPostureFloor) {
      posture = "BAD_POSTURE";
    }
  }

  Serial.print("{");
  Serial.print("\"id\":\"gilet_01\"");
  Serial.print(",\"timestamp\":"); Serial.print(timestamp);
  Serial.print(",\"activity\":\""); Serial.print(activity); Serial.print("\"");
  Serial.print(",\"posture\":\""); Serial.print(posture); Serial.print("\"");
  Serial.print(",\"angle_diff\":"); Serial.print(deltaAngle, 2);
  
  Serial.print(",\"sensorHigh\":{"); printData(a1, g1); Serial.print("}");
  Serial.print(",\"sensorLow\":{"); printData(a2, g2); Serial.print("}");
  Serial.println("}");

  delay(5000);
}